//pref
showGrayscale|bool|false
edgeMin|float|0|0.1|0.95
edgeMax|float|0.1|0.9|1
edgePower|float|0.05|1.5|10
//vert
void main() {
	gl_TexCoord[1] = gl_MultiTexCoord1;
	gl_Position = ftransform();
}
//frag
uniform int loops;
uniform float edgeMax, edgeMin, edgePower, stepSize, sliceSize, viewWidth, viewHeight;
uniform sampler3D gradientVol; //3DTexture containing gradient direction and magnitude
uniform sampler2D backFace;
uniform vec3 clearColor, clipPlane;
uniform float clipPlaneDepth;
uniform bool showGrayscale;
void main() {
	// get normalized pixel coordinate in view port (e.g. [0,1]x[0,1])
	vec2 pixelCoord = gl_FragCoord.st;
	pixelCoord.x /= viewWidth;
	pixelCoord.y /= viewHeight;	
	// starting position of the ray is stored in the texture coordinate
	vec3 start = gl_TexCoord[1].xyz;
	vec3 backPosition = texture2D(backFace,pixelCoord).xyz;
	vec3 dir = backPosition - start;
	float len = length(dir);
	dir = normalize(dir);
	vec3 surfaceColor = vec3(1.0,1.0,1.0);
	//next see if clip plane intersects ray
	if (clipPlaneDepth > -0.5) {
		gl_FragColor.rgb = vec3(1.0,0.0,0.0);
		//next, see if clip plane faces viewer
		bool frontface = (dot(dir , clipPlane) > 0.0);
		//next, distance from ray origin to clip plane
		float dis = dot(dir,clipPlane);
		if (dis != 0.0  )  dis = (-clipPlaneDepth - dot(clipPlane, start.xyz-0.5)) / dis;
		//we set "len = 0.0"  instead of "discard" or "return": return fails on 2006MacBookPro10.4ATI1900, discard fails on MacPro10.5NV8800 will not discard
		if ((frontface) && (dis >= len)) len = 0.0;
		if ((!frontface) && (dis <= 0.0)) len = 0.0;
		if ((dis > 0.0) && (dis < len)) {
			if (frontface) {
				start = start + dir * dis;
			} else {
				backPosition =  start + dir * (dis); 
			}
			dir = backPosition - start;
			len = length(dir);
			dir = normalize(dir);		
		}
	}	
	vec3 deltaDir = dir * stepSize;
	vec4 colorSample,colAcc = vec4(0.0,0.0,0.0,0.0);
	vec4 prevNorm = vec4(0.0,0.0,0.0,0.0);
	float lengthAcc = 0.0;
	float opacityCorrection = stepSize/sliceSize;
	//We need to calculate the ray's starting position: We add a random fraction of the stepsize to the original starting point to dither the output
	vec3 samplePos = start.xyz + deltaDir* (fract(sin(gl_FragCoord.x * 12.9898 + gl_FragCoord.y * 78.233) * 43758.5453));
	for(int i = 0; i < loops; i++) {
		colorSample = texture3D(gradientVol,samplePos);
		if (colorSample.a < prevNorm.a)
			colorSample.rgb = prevNorm.rgb;
		prevNorm = colorSample;
		colorSample.a = smoothstep(edgeMin, edgeMax, colorSample.a);		
		if (showGrayscale)
			colorSample.rgb = surfaceColor*colorSample.a;
		else 
			colorSample.rgb = abs(normalize(colorSample.rgb*2.0 - 1.0));
		colorSample.a = pow(colorSample.a,edgePower);	
		colorSample.a = 1.0-pow((1.0 - colorSample.a), opacityCorrection);		
		colorSample.rgb *= colorSample.a; 
		//accumulate color
		colAcc= (1.0 - colAcc.a) * colorSample + colAcc;
		samplePos += deltaDir;
		lengthAcc += stepSize;
		// terminate if opacity > 1 or the ray is outside the volume
		if ( lengthAcc >= len || colAcc.a > 0.98 )
			break;
	}
	if ((clearColor.r+clearColor.g+clearColor.b) > 1.5) colAcc.rgb = 1.0-colAcc.rgb;
	colAcc.rgb = mix(clearColor,colAcc.rgb,colAcc.a);
	if (len == 0.0) colAcc.rgb = clearColor;
	gl_FragColor = colAcc;
}