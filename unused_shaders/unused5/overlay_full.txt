//pref
overlayVolume|set|2
specular|float|0.0|0.3|1
shininess|float|0.01|10.0|30
edgeThresh|float|0.0|0.01|1
edgeExp|float|0.0|0.5|1
//boundExp|float|0.0|0.0|6
backAlpha|float|0.0|1|1
overDistance|float|0.0|0.3|1
overAlpha|float|0.0|1.2|2.0
overClip|bool|false
Overlay transparency independent of background.|note
//vert
void main() {
	gl_TexCoord[1] = gl_MultiTexCoord1;
	gl_Position = ftransform();
}
//frag
uniform int loops, overlays;
uniform float stepSize, sliceSize, viewWidth, viewHeight;
uniform sampler3D overlayVol;
uniform sampler3D overlayGradientVol;
uniform sampler3D intensityVol; //3DTexture containing brightness
uniform sampler3D gradientVol; //3DTexture containing gradient direction and magnitude
uniform sampler2D backFace;
uniform vec3 clearColor,lightPosition, clipPlane;
uniform float clipPlaneDepth;
uniform float specular, shininess, edgeThresh, edgeExp, overAlpha, overDistance, backAlpha;
uniform bool overClip;
void main() {
	float overAlphaFrac = overAlpha;
	if (overAlphaFrac > 1.0) overAlphaFrac = 1.0;
	float overLight = 1.0;
	if (overAlphaFrac > 1.0) overLight =  1.0 * ((overAlphaFrac - 1.5)/1.0);
	// get normalized pixel coordinate in view port (e.g. [0,1]x[0,1])
	vec2 pixelCoord = gl_FragCoord.st;
	pixelCoord.x /= viewWidth;
	pixelCoord.y /= viewHeight;	
	// starting position of the ray is stored in the texture coordinate
	vec3 start = gl_TexCoord[1].xyz;
	vec3 backPosition = texture2D(backFace,pixelCoord).xyz;
	vec3 dir = backPosition - start;
	float len = length(dir);
	dir = normalize(dir);
	float clipStart = 0.0;
	float clipEnd = len;
	//next see if clip plane intersects ray
	if (clipPlaneDepth > -0.5) {
		gl_FragColor.rgb = vec3(1.0,0.0,0.0);
		//next, see if clip plane faces viewer
		bool frontface = (dot(dir , clipPlane) > 0.0);
		//next, distance from ray origin to clip plane
		float dis = dot(dir,clipPlane);
		if (dis != 0.0  )  dis = (-clipPlaneDepth - dot(clipPlane, start.xyz-0.5)) / dis;
		if (overClip) {
			if ((frontface) && (dis >= len)) len = 0.0;
			if ((!frontface) && (dis <= 0.0)) len = 0.0;
			if ((dis > 0.0) && (dis < len)) {
				if (frontface) {
					start = start + dir * dis;
				} else {
					backPosition =  start + dir * (dis); 
				}
				dir = backPosition - start;
				len = length(dir);
				dir = normalize(dir);		
			}
		} else {
			if (frontface) clipStart = dis;
			if (!frontface)  clipEnd = dis;
		}
	}		
	vec3 deltaDir = dir * stepSize;
	vec4 overAcc = vec4(0.0,0.0,0.0,0.0);
	vec4 ocolorSample,colorSample,gradientSample,colAcc = vec4(0.0,0.0,0.0,0.0);
	float lengthAcc = 0.0;
	float overAtten = 0.0;
	int overDepth = 0;
	int backDepthEnd, backDepthStart = loops;
	//Jitter starting depth to reduce wood grain artifacts
	vec3 samplePos = start.xyz + deltaDir* (fract(sin(gl_FragCoord.x * 12.9898 + gl_FragCoord.y * 78.233) * 43758.5453));
	vec4 prevNorm = vec4(0.0,0.0,0.0,0.0);
	vec4 oprevNorm = vec4(0.0,0.0,0.0,0.0);
		float opacityCorrection = stepSize/sliceSize;
	vec3 lightDirHeadOn =  normalize(gl_ModelViewMatrixInverse * vec4(0.0,0.0,1.0,0.0)).xyz ;
	float stepSizex2 = clipStart + (stepSize * 2.0);
	for(int i = 0; i < loops; i++) {
		if ((lengthAcc <= clipStart) || (lengthAcc > clipEnd)) {
			colorSample.a = 0.0;
		} else {
			colorSample = texture3D(intensityVol,samplePos);
			if ((lengthAcc <= stepSizex2) && (colorSample.a > 0.01) )  colorSample.a = sqrt(colorSample.a); //opaque clipping surface
			colorSample.a = 1.0-pow((1.0 - colorSample.a), opacityCorrection);
			//colorSample.a = 1.0-pow((1.0 - colorSample.a), stepSize/sliceSize);//opacity correction
			if ((colorSample.a > 0.01) && (lengthAcc > stepSizex2)  ) {	
				if (backDepthStart == loops) backDepthStart = i;
				backDepthEnd = i;	
				gradientSample= texture3D(gradientVol,samplePos); //interpolate gradient direction and magnitude
				gradientSample.rgb = normalize(gradientSample.rgb*2.0 - 1.0); //direction saved as 0..1, rescale to -1..1
				//re-use previous normal if it has larger magnitude
				if (gradientSample.a < prevNorm.a)
					gradientSample.rgb = prevNorm.rgb;
				prevNorm = gradientSample;
				//Edge shading - darken edges parallel with viewing direction
				float lightNormDot = dot(gradientSample.rgb, lightDirHeadOn); //with respect to viewer
				float edgeVal = pow(1.0-abs(lightNormDot),edgeExp);
				edgeVal = edgeVal * pow(gradientSample.a,0.3);
				if (edgeVal >= edgeThresh) 
					colorSample.rgb = mix(colorSample.rgb, vec3(0.0,0.0,0.0), pow((edgeVal-edgeThresh)/(1.0-edgeThresh),4.0));
				//specular
				lightNormDot = dot(gradientSample.rgb, lightPosition); //with respect to light location
				if (lightNormDot > 0.0)
					colorSample.rgb +=   specular * pow(max(dot(reflect(lightPosition, gradientSample.rgb), dir), 0.0), shininess);
			}
		}
		

		if ( overlays > 0 ) {
			gradientSample= texture3D(overlayGradientVol,samplePos); //interpolate gradient direction and magnitude
			if (gradientSample.a > 0.01) {			
				if (overDepth == 0) overDepth = i;
				if (gradientSample.a < oprevNorm.a)
					gradientSample.rgb = oprevNorm.rgb;
				oprevNorm = gradientSample;
				gradientSample.rgb = normalize(gradientSample.rgb*2.0 - 1.0); //direction saved as 0..1, rescale to -1..1
				ocolorSample = texture3D(overlayVol,samplePos);
				ocolorSample.a *= gradientSample.a; //modulate by gradient
				ocolorSample.a *= overAlphaFrac;
				ocolorSample.a = sqrt(ocolorSample.a);
				//specular
				float lightNormDot = dot(gradientSample.rgb, lightPosition); //with respect to light location
				if (lightNormDot > 0.0)
					ocolorSample.rgb +=   overLight * specular * pow(max(dot(reflect(lightPosition, gradientSample.rgb), dir), 0.0), shininess);
				float lightAmount = 0.1 * overLight;
				//diffuse
		  		if (lightAmount > 0.0)
					ocolorSample.rgb += lightAmount*dot(normalize(lightPosition), gradientSample.rgb);								
				colorSample= max(ocolorSample, colorSample);
				//accumulate overlay color
				ocolorSample.a = 1.0-pow((1.0 - ocolorSample.a), opacityCorrection);		
				overAcc= (1.0 - overAcc.a) * ocolorSample + overAcc;
			}
		}
		
		colorSample.rgb *= colorSample.a; 
		//accumulate color
		colAcc= (1.0 - colAcc.a) * colorSample + colAcc;
		samplePos += deltaDir;
		lengthAcc += stepSize;
		if ( lengthAcc >= len  )
			break;
	}
	colAcc*=backAlpha;
	if ((overAcc.a > 0.0) && (overAlpha > 1.0))  {
		colAcc.a=max(colAcc.a,overAcc.a);
		if ((overDistance > 0.0) && (overDepth > backDepthStart) && (backDepthEnd > backDepthStart)) {
			if (overDepth > backDepthEnd) overDepth = backDepthStart; // backDepthEnd
			float dx = float(overDepth-backDepthStart)/ float(backDepthEnd - backDepthStart);
			dx = pow(1.0-dx, overDistance);
			dx = pow(dx, 2.0);
			overAcc *= dx;
		}
		overAlphaFrac = overAlpha - 1.0;
		colAcc.rgb=mix(colAcc.rgb, overAcc.rgb,  overAlphaFrac);
	}
	if ( colAcc.a < 1.0 )
		colAcc.rgb = mix(clearColor,colAcc.rgb,colAcc.a);
	if (len == 0.0) colAcc.rgb = clearColor;
	gl_FragColor = colAcc;
}